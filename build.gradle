
def ASSERTJ_GENERATOR_VERSION = '2.0.0'

group 'net.navatwo.org.assertj'
version ASSERTJ_GENERATOR_VERSION + '-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'java-gradle-plugin' // for test kit

sourceCompatibility = '1.8'

/*
  The configuration below shows the minimum properties
  required to publish to the plugin portal
*/
pluginBundle {
    website = 'https://github.com/Nava2/assertj-generator-gradle-plugin'
    vcsUrl = 'https://github.com/Nava2/assertj-generator-gradle-plugin'
    description = """Run the AssertJ generator against your source files to generate test sources."""
    tags = ['assertj', 'test', 'generator']

    plugins {
        assertJGeneratorPlugin {
            id = 'net.navatwo.assertj.generator.gradle.plugin'
            displayName = 'AssertJ Generator Plugin'
        }
    }
}

// Used by Gradle TestKit
gradlePlugin {
    plugins {
        assertJGeneratorPlugin {
            id = "net.navatwo.assertj.generator.gradle.plugin"
            implementationClass = "org.assertj.generator.gradle.AssertJGeneratorGradlePlugin"
        }
    }
}

configurations {
    gradleRuntime
}


test {
    testLogging {
        // Make sure output from
        // standard out or error is shown
        // in Gradle output.
        showStandardStreams = true
    }
}

idea {
    module {
        scopes.RUNTIME.plus += [ configurations.gradleRuntime ]
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    // https://mvnrepository.com/artifact/org.assertj/assertj-core
    compile group: 'org.assertj', name: 'assertj-core', version: '3.8.0'

    // https://mvnrepository.com/artifact/org.assertj/assertj-assertions-generator
    compile group: 'org.assertj', name: 'assertj-assertions-generator', version: ASSERTJ_GENERATOR_VERSION

    // https://mvnrepository.com/artifact/com.google.guava/guava
    compile group: 'com.google.guava', name: 'guava', version: '20.0'

    testCompile gradleTestKit()
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }

    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.6"
        classpath "gradle.plugin.org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.3.2"
    }
}
